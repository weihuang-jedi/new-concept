load "utils.ncl"

;----------------------------------------------------------------------
 info = readinfo("DATAINFO")

 plottype = info(0)
 dirname = info(1)
 finame = info(2)
 foname = info(3)
 imgname = info(4)
 title = info(5)

 fi = dirname+finame
 fo = dirname+foname

 print("fi = " + fi)
 print("fo = " + fo)

 f = addfile(fi, "r")

 u = f->UGRD_P0_L100_GLL0(:,:,:)  ; m/s, (level,lat,lon)
 v = f->VGRD_P0_L100_GLL0(:,:,:)
 q = f->SPFH_P0_L100_GLL0(:,:,:)

;Divergence function [used later] requires S->N grid order

 u  = u(:,::-1,:)  
 v  = v(:,::-1,:)
 q  = q(:,::-1,:)

 uq = u*q
 vq = v*q

 scale = 1.0e6

;----------------------------------------------------------------------
;Divergence of moisture flux: uv2dvF => global 'fixed' rectilinear grid

 qfdiv  = uv2dvF_Wrap(uq, vq)    ; (level,lat,lon)
 qfdiv = scale*qfdiv        ; keep meta data                         
 copy_VarCoords(u, qfdiv)
 qfdiv@long_name = "Moisture Flux Divergence"
 qfdiv@units     = "1.0e6*1/s"

 print("-----")
 print("qfdiv")
 printVarSummary(qfdiv)
 printMinMax(qfdiv, 0)

 write_div(qfdiv, fo)

;--------------------------------------------------------------------------------------
 wks   = gsn_open_wks(plottype, imgname)        ; send graphics to PNG file

 res                   = True             ; plot mods desired
 res@gsnDraw           = True
 res@gsnFrame          = True
 res@lbLabelBarOn      = True        

 res@cnFillOn          = True             ; turn on color
 res@cnLinesOn         = False            ; turn off contour lines
 res@cnLineLabelsOn    = False            ; turn off contour lines
 res@cnFillPalette     = "ViBlGrWhYeOrRe" ; set White-in-Middle color map
 res@lbLabelBarOn      = False            ; turn off individual cb's
 res@mpFillOn          = False            ; turn off map fill
                                          ; Use a common scale
 res@cnLevelSelectionMode = "ManualLevels"; manual set levels so lb consistent
 res@cnMaxLevelValF       = 10.0                  ; min level
 res@cnMinLevelValF       = -res@cnMaxLevelValF  ; min level
 res@cnLevelSpacingF      =  1.0                  ; contour interval
 res@tiMainString         = title

;--------------------------------------------------------------------------------------
;Plot Divergence

 pvar := qfdiv(40, :, :)
 plt = gsn_csm_contour_map(wks,pvar,res)

